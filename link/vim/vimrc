" --------- "
"  Plugins  "
" --------- "

" Vim-Plug. Run :PlugUpdate to install or update plugins.
call plug#begin('~/.vim/vimplug')
Plug 'scrooloose/nerdtree'
Plug 'junegunn/fzf'
Plug 'junegunn/fzf.vim'
Plug 'cespare/vim-toml', { 'branch': 'main' }
Plug 'sirver/ultisnips'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
Plug 'matze/vim-move'
Plug 'keith/swift.vim'
Plug 'hashivim/vim-terraform'
call plug#end()

" ------------------ "
"  General Settings  "
" ------------------ "

" Sane behaviour for <BS> and Ctrl-H in insert mode.
" (Delete indents, line endings, and beyond the insert point.)
set backspace=indent,eol,start

" Allow Ctrl-D to forward-delete in insert mode.
inoremap <C-d> <Del>

" Turn on syntax highlighting.
syntax enable

" Colorscheme.
colorscheme dusk
autocmd BufRead * :colorscheme dusk

" The default is 8 commands.
set history=9999

" Allow hidden buffers.
set hidden

" Indentation.
set autoindent          " Copy new-line indentation from the previous line.
set breakindent         " Maintain visual indent when wrapping lines.
"set smartindent        " Insert extra level of indentation in some cases.
set cindent             " Alternative to smartindent.

" Load language plugins and indent files.
filetype plugin on
filetype indent on

" Tabs.
set expandtab           " Insert spaces instead of tabs when the <tab> key is pressed.
set tabstop=4           " Number of spaces to insert, also display-with for <tab> characters.
set shiftwidth=4        " Autoindent size, also determines the operation of >> and ==.
set shiftround          " Align indents to a multiple of 'shiftwidth'.
set smarttab            " <BS> will delete a 'shiftwidth' worth of spaces at line start.

" Alternative to using the escape key.
"inoremap jj <esc>

" Shortcuts for navigating splits.
" nnoremap <C-J> <C-W><C-J>
" nnoremap <C-K> <C-W><C-K>
" nnoremap <C-H> <C-W><C-H>
" nnoremap <C-L> <C-W><C-L>

" Open new split panes to the right and to the bottom.
set splitbelow
set splitright

" Set custom filetypes.
autocmd BufNewFile,BufRead *.sh set filetype=bash
autocmd BufNewFile,BufRead *.txt set filetype=text
autocmd BufNewFile,BufRead *.stx set filetype=syntext
autocmd BufNewFile,BufRead *.pyro set filetype=pyro
autocmd BufNewFile,BufRead *.h set filetype=c
autocmd BufNewFile,BufRead go.mod set filetype=gomod
autocmd BufNewFile,BufRead go.sum set filetype=gosum

" Use zsh highlighting for all shell scripts as the sh/bash highlighters are broken.
" autocmd BufNewFile,BufRead *.sh set filetype=zsh

" Enable mouse support.
set mouse=a

" Turn off modelines.
set nomodeline

" Assume .tex files are latex by default.
let g:tex_flavor = "latex"

" Insert a blank line below the cursor in normal mode.
nnoremap <Enter> :call append(line('.'), '')<CR>

" Insert a blank line above the cursor in normal mode.
" (Won't work in terminals.)
nnoremap <S-Enter> :call append(line('.')-1, '')<CR>

" Show as much of the last line as possible when wrapping is enabled.
set display=lastline

" Clear the splash screen.
set shortmess+=I

" Enable italics in Mac Terminal.
let &t_ZH="\e[3m"
let &t_ZR="\e[23m"

" Highlight the current line.
set cursorline

" Create the cache directory if it doesn't already exist.
if !isdirectory($HOME . "/.cache/vim")
    call mkdir($HOME . "/.cache/vim", "p")
endif

" Relocate backup files, swap files, and undo files to a central location.
set backupdir=~/.cache/vim//
set directory=~/.cache/vim//
set undodir=~/.cache/vim//

" Exit from visual mode immediately after hitting the <esc> key.
set ttimeoutlen=0

" Recognize 0b and 0x as prefixes for binary and hex integers.
" Do not treat leading zeros as indicating octal.
set nrformats=bin,hex

" In command mode, make ctrl-p and ctrl-n match the existing prefix when
" scrolling backwards and forwards through the command history. The simplest
" way to achieve this is to map to the up and down keys which already have
" this behaviour:
"
"   cnoremap <C-P> <Up>
"   cnoremap <C-N> <Down>
"
" The additional code below is to preserve existing ctrl-p and ctrl-n
" behaviour in wildmenus.
cnoremap <expr> <C-P> wildmenumode() ? "\<C-P>" : "\<Up>"
cnoremap <expr> <C-N> wildmenumode() ? "\<C-N>" : "\<Down>"

" Show a vertical guideline for text width.
set colorcolumn=100

" Show line numbers.
set number

" Use the system clipboard as the unnamed (i.e. default) register.
set clipboard=unnamed

" Autowrapping and formatting using 'gq'. See :help gq for details.
" t -- auto-wrap text using textwidth.
" c -- auto-wrap comments using textwidth, inserting current comment leader.
" r -- auto-insert current comment leader on <enter> in insert mode.
" o -- auto-insert current comment leader after 'o' or 'O' in normal mode.
" q -- allow formatting of comments with 'gq'.
" l -- already long lines are not broken in insert mode.
set formatoptions=crq
set textwidth=100

" Hard wrap lines at 100 characters.
" autocmd BufNewFile,BufRead *.txt setlocal textwidth=100
" autocmd BufNewFile,BufRead *.stx setlocal textwidth=100

" Show tabs.
set list
set listchars=tab:\|->,trail:Â·

" Set the modifier key for the vim-move plugin to Ctrl.
let g:move_key_modifier = 'C'

" Make autocompletion case-insensitive.
au InsertEnter * set ignorecase
au InsertLeave * set noignorecase

" Set preferred cindent indentation-style for case statements.
set cinoptions=l1

" Don't highlight {} inside brackets as an error.
let c_no_curly_error=1

" Reselect pasted text.
nnoremap gp `[v`]

" Auto-generate the tags file on file write of *.c and *.h files.
" autocmd BufWritePost *.c,*.h silent! !ctags -R . &

" Jump to the outermost enclosing {.
nnoremap [[ 99[{

" Jump to the outermost enclosing }.
noremap ]] 99]}

" Make Y yank to the end of the line so it matches C and D.
nnoremap Y y$

" Run goimports automatically after a *.go file is saved.
augroup autorun_goimports
    autocmd!
    autocmd BufWritePost *.go silent !goimports -w %
    autocmd BufWritePost *.go edit
    autocmd BufWritePost *.go redraw!
    autocmd BufWritePost *.go normal! zz
augroup end

" -------- "
"  Search  "
" -------- "

" Find the next match as the search term is being typed.
set incsearch

" Highlight search matches.
set hlsearch

" Automatically center matches on the screen.
nnoremap n nzz
nnoremap N Nzz
nnoremap * *zz
nnoremap # #zz

" --------------- "
"  Finding Files  "
" --------------- "

" Set path to the directory of the current file, plus the current working
" directory and all subfolders.
set path=.,**

" Display all matching files on tab-complete.
set wildmenu
set wildignorecase
set wildmode=longest:list,full

" ----------- "
"  Scrolling  "
" ----------- "

" Vertical scroll offset.
set scrolloff=1

" Horizontal scroll rate. The default is 0 which scrolls by half a screen.
set sidescroll=5

" Buffer scrolling with ctrl-n and ctrl-p. (By default they act just like j and k.)
nnoremap <C-N> <C-E>gj
nnoremap <C-P> <C-Y>gk

" ------------------ "
"  Leader Shortcuts  "
" ------------------ "

" Remap the <leader> key for custom shortcuts.
let mapleader="\<space>"

" Stop highlighting the last search term. Highlighting will be re-enabled for
" the next search.
nnoremap <leader><space> :noh<cr>

" Alternate buffer. Swap back and forth between the latest buffers.
nnoremap <leader>a :b#<cr>

" Change the word under the cursor, then change subsequent occurances by hitting dot.
" Can use 'n' to preview or skip over the next match.
"   *   -- search for the word under the cursor
"   ``  -- jump back to the first instance
"   c   -- operation: change
"   gn  -- motion: perform the operation on the next match
nnoremap <leader>c *``cgn

" Open the directory containing the current file.
nnoremap <leader>d :!open "%:p:h"<cr><cr>

" Edit the vimrc file.
nnoremap <leader>e :e ~/.vim/vimrc<cr>

" FZF - fuzzy file finder.
nnoremap <leader>fb :Buffers<cr>
nnoremap <leader>ff :Files<cr>
nnoremap <leader>fm :Marks<cr>
nnoremap <leader>fg :Rg<cr>
nnoremap <leader>fs :Filetypes<cr>
nnoremap <leader>ft :Tags<cr>

" Swap between .c and .h files in the same directory.
" (% is the current filename, :r removes the extension.)
nnoremap <leader>gh :e %:r.h<cr>
nnoremap <leader>gc :e %:r.c<cr>

" Navigate splits.
nnoremap <leader>h <C-W><C-H>
nnoremap <leader>j <C-W><C-J>
nnoremap <leader>k <C-W><C-K>
nnoremap <leader>l <C-W><C-L>

" Save all open files, clear the screen, and run make.
nnoremap <leader>m :wa <bar> !clear -x && make<cr>

" Toggle between relative and absolute line numbering.
nnoremap <silent><leader>n :set relativenumber!<cr>

" Toggle NerdTree file browser on and off.
nnoremap <leader>o :NERDTreeToggle<cr>

" Reload the vimrc file and redraw the screen.
nnoremap <leader>r :source ~/.vim/vimrc <bar> noh <bar> redraw<cr>

" Toggle spellcheck.
nnoremap <leader>s :set spell!<cr>

" Identify the syntax highlighting group under the cursor.
nnoremap <leader>sh :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<' . synIDattr(synID(line("."),col("."),0),"name") . "> lo<" . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<cr>

" Open a terminal window.
" nnoremap <leader>t :term <cr><C-w>:res 50<cr>

" Generate/regenerate the tags file.
nnoremap <leader>t :wa <bar> !uctags --recurse --exclude=.git --exclude=docs . &<cr><cr>

" Toggle word-wrapping.
nnoremap <leader>w :call ToggleWrap()<CR>

" Save all open files, clear the screen, and execute the current file.
nnoremap <leader>x :wa <bar> !clear -x && "%:p"<cr>

" ------------------------------------- "
"  File-Type Specific Leader Shortcuts  "
" ------------------------------------- "

" Latex: save all open files and compile 'main.tex'.
autocmd FileType tex,bib nmap <buffer> <leader>m :wa <bar> !latexmk --xelatex main && mv main.pdf output.pdf<cr>

" Latex: open the compiled pdf.
autocmd FileType tex,bib nmap <buffer> <leader>v :!open -a skim output.pdf<cr><cr>

" --------------- "
"  Word Wrapping  "
" --------------- "

" Wrap lines at word boundaries.
set linebreak

let s:wrapenabled = 0
set nowrap

function! ToggleWrap()
    set wrap!
    if s:wrapenabled
        unmap j
        unmap k
        unmap 0
        unmap ^
        unmap $
        let s:wrapenabled = 0
    else
        nnoremap j gj
        nnoremap k gk
        nnoremap 0 g0
        nnoremap ^ g^
        nnoremap $ g$
        vnoremap j gj
        vnoremap k gk
        vnoremap 0 g0
        vnoremap ^ g^
        vnoremap $ g$
        let s:wrapenabled = 1
    endif
endfunction

call ToggleWrap()

" ----------------- "
"  Trim Whitespace  "
" ----------------- "

function! TrimWhitespace()
    let l:save = winsaveview()
    keeppatterns %s/\s\+$//e
    call winrestview(l:save)
endfunction

autocmd BufWritePre * :call TrimWhitespace()

" ------------- "
"  Status Line  "
" ------------- "

" Always show the status line.
set laststatus=2

" Show the current command as we're typing it.
set showcmd

set statusline=%#StatusA4#%{&modified?'\ \ \ '.bufnr('%').'\ \ ':''}%#StatusA1#%{&modified?'':'\ \ \ '.bufnr('%').'\ \ '}%#StatusA2#\ \ %<%f\ \ %{&modified?'[+]\ \ ':''}%#StatusA3#%=%4l/%L\ \ %3v\ \ 0x%04B\ \ %{&fo}:%{&tw}\ \ %{&fenc?&fenc:&enc}\ \ %{&ff}\ \ %y\ %{'\ '}

augroup select_statusline
    autocmd!
    autocmd WinEnter,BufEnter * :setlocal statusline=%#StatusA4#%{&modified?'\ \ \ '.bufnr('%').'\ \ ':''}%#StatusA1#%{&modified?'':'\ \ \ '.bufnr('%').'\ \ '}%#StatusA2#\ \ %<%f\ \ %{&modified?'[+]\ \ ':''}%#StatusA3#%=%4l/%L\ \ %3v\ \ 0x%04B\ \ %{&fo}:%{&tw}\ \ %{&fenc?&fenc:&enc}\ \ %{&ff}\ \ %y\ %{'\ '}
    autocmd WinLeave * :setlocal statusline=%#StatusI1#\ \ %n\ \ %#StatusI2#\ \ %<%f\ \ %{&modified?'[+]\ \ ':''}%#StatusI3#%=%4l/%L\ \ %3v\ \ 0x%04B\ \ %{&fo}:%{&tw}\ \ %{&fenc?&fenc:&enc}\ \ %{&ff}\ \ %y\ %{'\ '}
augroup end

" ---------- "
"  Open URL  "
" ---------- "

" Source: https://vimtricks.com/p/open-url-under-cursor/

function! OpenURLUnderCursor()
    let s:uri = expand('<cWORD>')
    let s:uri = substitute(s:uri, '?', '\\?', '')
    let s:uri = shellescape(s:uri, 1)
    if s:uri != ''
        silent exec "!open '".s:uri."'"
        :redraw!
    endif
endfunction

nnoremap gx :call OpenURLUnderCursor()<CR>

" ---------------------- "
"  Plugin: vim-surround  "
" ---------------------- "

" Use 'gs' as the insert-surround command as I can never remember 'ys'.
nmap gs ys

" ------------------- "
"  Plugin: ultisnips  "
" ------------------- "

let g:UltiSnipsExpandTrigger = '<tab>'
let g:UltiSnipsJumpForwardTrigger = '<tab>'
let g:UltiSnipsJumpBackwardTrigger = '<s-tab>'

" ------------------ "
"  Plugin: NERDTree  "
" ------------------ "

" Close NT when opening a file.
let NERDTreeQuitOnOpen = 1

" Nicer UI.
let NERDTreeMinimalUI = 1
let NERDTreeDirArrows = 1

" Set the default width.
let NERDTreeWinSize = 60

" Show hidden files by default. (Shift-I toggles the display of hidden files.)
let NERDTreeShowHidden=1

" Don't show files matching any of the following regexes.
let NERDTreeIgnore = ['\~$', '\.DS_Store$']

" Close Vim if the only window left is NT.
autocmd bufenter * if (winnr("$") == 1 && exists("b:NERDTree") && b:NERDTree.isTabTree()) | q | endif
